# Copyright (C) 2008, The Perl Foundation.
# $Id$

=head1 NAME

languages/markdown/t/harness - A harness for Parrot Markdown

=head1 SYNOPSIS

    cd languages && perl -I../lib -Imarkdown/t markdown/t/harness --files

    cd languages && perl -I../lib -Imarkdown/t markdown/t/harness

    cd languages && perl -I../lib -Imarkdown/t markdown/t/harness markdown/t/examples.t

=head1 DESCRIPTION

If I'm called with a single
argument of "--files", I just return a list of files to process.
This list is one per line, and is relative to the languages dir.

If I'm called with no args, I run the complete suite.

Otherwise I run the tests that were passed on the command line.

=cut

use strict;
use warnings;
use lib '..';

use Cwd();
use Data::Dumper;
use File::Spec;
use Getopt::Long;
use Test::Harness();

my $language = 'markdown';

my $opt_files;
GetOptions(
    'files' => \$opt_files,
);

if ( $opt_files ) {

    # Only the Makefile in 'parrot/languages' uses --files
    my $dir = File::Spec->catfile( $language, 't' );
    push my @files, glob( File::Spec->catfile( $dir, '*.t' ) );
    print join( "\n", @files );
    print "\n" if scalar(@files);
}
else {
    my @files;

    if ( scalar(@ARGV) ) {

        # Someone specified tests for me to run.
        @files = grep { -f $_ } @ARGV;
    }
    else {
        ( undef, undef, my $current_dir )
            = File::Spec->splitpath( Cwd::getcwd() );
        my $dir;
        if ( $current_dir eq 'languages' ) {
            $dir = File::Spec->catfile( $language, 't' );
        }
        elsif ( $current_dir eq $language ) {
            $dir = 't';
        }
        if ($dir) {
            @files = glob( File::Spec->catfile( $dir, '*.t' ) );
        }
    }

    Test::Harness::runtests(@files) if scalar(@files);
}

=head1 HISTORY

Mostly taken from F<languages/lua/t/harness>.

=head1 AUTHOR

Francois Perrad

=cut

# Local Variables:
#   mode: cperl
#   cperl-indent-level: 4
#   fill-column: 100
# End:
# vim: expandtab shiftwidth=4:

